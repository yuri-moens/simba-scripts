program OGLAlcher;
{$i cLib.simba}

{ This script requires ogLib. Learn how to set it up correctly here:
  https://villavu.com/forum/showthread.php?t=112486

  This script also requires cLib. Learn how to set it up correctly here:
  https://villavu.com/forum/showthread.php?t=113750

  Make sure to set your game window to 'Resizable' and your interface to the
  RuneScape 3 default. Also turn on 'Show XP tracker' at the Interface settings.

  Setup the script at lines 33-36, setup alerting, screen-size and debug options at lines 41-47.

  Setup
  =====
  1. Make sure the preferred alchemy spell is on your action bar. }

type
  tAlchTask = record
    slot, amount: integer;
  end;

{ const Script Constants;
  Description: Constants necessary for the script to work. Should not be changed. }
const
  LOW_ALCH = 64692;
  HIGH_ALCH = 79489;

{ const User Constants;
  Description: Change these to your own preference. }
const
  ALCH_TYPE = HIGH_ALCH; // options: LOW_ALCH or HIGH_ALCH
  ALCH_TASKS : array[0..0] of tAlchTask = [[1, 1911]]; // add more tasks between the square brackets like so: [slot, amount], don't forget to update array[0..0] to 0..1 as well (or 0..2, 0..3, ... depending on the amount of tasks)
  // an example for alching 100 items in slot 1 and 200 in slot 2: ALCH_TASKS : array[0..1] of tAlchTask = [[1, 100], [2, 200]];
  METEORITE_SKILL = MAGIC;

{ const Misc Constants;
  Description: Miscellaneous constants for the script. }
const
  WIDTH = 1440;
  HEIGHT = 900;
  GLX_DEBUG = 'none'; // options: none, models, textures, fonts
  VERSION = '1.1.1';
  ALERT_FLAG = false;
  ALERT_KEYWORD = 'Username';
  ALERT_SOUND = 'C:\Simba\sound.wav';

var
  clientCenter, itemPoint: tPoint;
  alchActionBar: char;
  startTime, startExperience, playerId, alchCasts, totalAlchCasts, clickCount, clickMax, taskIndex: integer;
  canPrintProgress, canAlert, canAlch: tCountDown;

procedure printProgress();
var
  elapsedTime: integer;
  hours: double;
  minutes: double;
  gainedExperience: integer;
begin
  elapsedTime := getSystemTime() - startTime;
  hours := elapsedTime / 3600000;
  minutes := floor(elapsedTime / 60000) mod 60;
  gainedExperience := player.getExperience() - startExperience;

  clearDebug();

  writeln('=========================================');
  writeln('Running for: ', floor(hours), 'h ', minutes, 'm');
  writeln('Alch casts: ', totalAlchCasts);
  writeln('Alch casts per hour: ', floor(totalAlchCasts / hours));
  writeln('Experience earned: ', gainedExperience);
  writeln('Experience per hour: ', floor(gainedExperience / hours));
  writeln('=========================================');

  canPrintProgress.setTime(60000);
end;

procedure twiddleThumbs;
begin
  if ALERT_FLAG and canAlert.isFinished() then
  begin
    if antiban.alert(ALERT_KEYWORD) then
    begin
      playSound(ALERT_SOUND);
      canAlert.setTime(30000);
    end;
  end;

  antiban.checkRandoms();

  case random(533) of
    1: antiban.rotateCamera(342, 11, 4);
  end;
end;

procedure setupActionBarVariable();
begin
  alchActionBar := actionBar.getActionKey(ALCH_TYPE);
end;

function isDone(): boolean;
begin
  result := alchCasts >= ALCH_TASKS[taskIndex].amount;
end;

procedure alch();
begin
  typeSend(alchActionBar, false);

  if (clickMax = 0) or (clickCount = clickMax) then
  begin
    itemPoint := inventory.getSlot(ALCH_TASKS[taskIndex].slot - 1).randomizePointEllipse(28);
    clickMax := normalRandom(200, 1200);
  end;

  wait(normalRandom(0, 250));

  mouse.click(itemPoint);
  inc(clickCount);
  inc(alchCasts);
  inc(totalAlchCasts);
  canAlch.setTime(normalRandom(2600, 3100));
end;

procedure mainLoop();
var
  funcReaction: procedure() = @twiddleThumbs;
begin
  if (isDone()) then
  begin
    writeln('Finished alching ', ALCH_TASKS[taskIndex].amount, ' items in slot ', ALCH_TASKS[taskIndex].slot, '.');

    if (taskIndex = length(ALCH_TASKS) - 1) then
    begin
      writeln('Logging out...');
      login.logout();
      terminateScript();
    end
    else
    begin
      inc(taskIndex);
      alchCasts := 0;
      clickCount := 0;
      clickMax := 0;
    end;
  end;

  if (canAlch.isFinished()) and (not player.isAnimating(playerId)) then
    funcReaction := @alch
  else if (canPrintProgress.isFinished()) then
    funcReaction := @printProgress;

  funcReaction();
end;

begin
  ogl.setup(WIDTH, HEIGHT);
  clientCenter := ogl.getClientMidPoint();
  startTime := getSystemTime();
  ogl.setDebugMode(GLX_DEBUG);
  cLib.selfUpdate();

  if (not web.isUpToDate(VERSION, 'https://raw.githubusercontent.com/yuri-moens/simba-scripts/master/OGLAlcher.version')) then
    web.update('https://raw.githubusercontent.com/yuri-moens/simba-scripts/master/OGLAlcher.version', 'OGLAlcher');

  writeln('[OGL] Alcher v', VERSION, ' by ChaosEnergy');
  writeln('=========================================');

  try
    antiban.setMeteoriteSkill(METEORITE_SKILL);
    playerId := player.getId();
    startExperience := player.getExperience();
    setupActionBarVariable();

    repeat
      mainLoop;
      wait(normalRandom(220, 670));
    until false;
  except
    writeln('Oops! It appears I have crashed.');
    login.logout();
    terminateScript();
  end;
end.